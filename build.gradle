apply from: 'http://tellurianring.com/projects/gradle-plugins/gradle-release/apply.groovy'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    group = "com.github.tsouza.promises"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
    }

    if (System.getenv("CI") == null) {
        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from javadoc
        }

        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        artifacts {
            archives javadocJar, sourcesJar
        }

        signing {
            sign configurations.archives
        }

        uploadArchives {
            repositories {
                mavenDeployer {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    pom.project {
                        name project.name
                        packaging 'jar'
                        url 'https://github.com/tsouza/rxjava-promises-aplus'

                        scm {
                            url 'scm:git@github.com:tsouza/rxjava-promises-aplus.git'
                            connection 'scm:git@github.com:tsouza/rxjava-promises-aplus.git'
                            developerConnection 'scm:git@github.com:tsouza/rxjava-promises-aplus.git'
                        }

                        licenses {
                            license {
                                name 'The MIT License (MIT)'
                                url 'http://opensource.org/licenses/MIT'
                            }
                        }

                        developers {
                            developer {
                                id 'tsouza'
                                name 'Thiago Souza'
                                email 'tcostasouza@gmail.com'
                            }
                        }
                    }
                }
            }
        }
    }
}
